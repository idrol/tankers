apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = "net.tankers.main.Main"
applicationDefaultJvmArgs = ["-Djava.library.path=native/"]
sourceCompatibility = 1.8
version = '0.2'

task moreStartScripts(type: CreateStartScripts) {
    mainClassName = "net.tankers.server.Server"
    applicationName = "Server"
	defaultJvmOpts = ["-Djava.library.path=native/windows/"]
    outputDir = new File(project.buildDir, 'scripts')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
            from(moreStartScripts)
            fileMode = 0755
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Tankers',
                   'Implementation-Version': version
    }
}

startScripts {
	doLast {
		windowsScript.text = windowsScript.text.replace('native/', '%~dp0../native/windows/')
		windowsScript.text = windowsScript.text.replace('-DOgpResources=', '-DOgpResources=../')
		unixScript.text = unixScript.text.replace('native/', '\$APP_HOME/native/')
		unixScript.text = unixScript.text.replace('-DOgpResources=', '-DOgpResources=../')
	}
}

distZip {
	into(project.name+"-"+project.version) {
		from '.'
		include 'native/**'
		include 'res/**'
	}
}

distTar {
	into(project.name+"-"+project.version) {
		from '.'
		include 'native/*'
		include 'res/**'
	}
}

repositories {
    mavenCentral()
	maven { 
		url "https://clojars.org/repo"
		url 'http://nifty-gui.sourceforge.net/nifty-maven-repo'
	}
}

dependencies {
	compile 'org.lwjgl.lwjgl:lwjgl:2.9.3'
	compile 'org.jbox2d:jbox2d:2.1.2.2'
	compile 'io.netty:netty-all:4.0.36.Final'
	compile 'lessvoid:nifty:1.4.1'
    compile 'lessvoid:nifty-default-controls:1.4.1'
    compile 'lessvoid:nifty-style-black:1.4.1'
	compile 'lessvoid:nifty-lwjgl-renderer:1.4.1'
	compile 'org.xerial:sqlite-jdbc:3.8.11.2'
	compile 'org.jbox2d:jbox2d-library:2.2.1.1'
	compile 'org.slick2d:slick2d-core:1.0.1'
	// http://mvnrepository.com/artifact/de.svenkubiak/jBCrypt
	compile group: 'de.svenkubiak', name: 'jBCrypt', version: '0.4.1'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}
